   10 REM HELI Version 1.7 By Phil Hite 2021
   20 REM IF PAGE>&E00 GOTO 2160
   30 MODE 2
   40 VDU 23,1,0;0;0;0;
   50 VDU 23,240,255,128,65,34,20,8,255,0
   60 VDU 23,241,170,85,170,85,170,85,170,85
   70 VDU 23,242,24,36,24,36,24,36,24,36
   80 VDU 23,243,2,0,28,186,254,190,28,0
   90 VDU 23,244,128,0,112,186,254,250,112,0
  100 VDU 23,245,60,126,255,231,231,231,165,231
  110 VDU 23,246,7,0,0,128,127,1,0,0
  120 VDU 23,247,255,32,112,236,226,252,80,252
  130 VDU 23,248,255,4,14,55,71,63,10,63
  140 VDU 23,249,224,0,0,1,254,128,0,0
  150 VDU 23,250,170,170,170,170,254,214,124,56
  160 DIM G(19):H%=0:A%=-15:R%=A%:Q%=1:DIM H(9):DIM L(9):DIM N$(9,2):FOR Y%=0 TO 9:L(Y%)=1:FOR X%=0 TO 2:N$(Y%,X%)="A":NEXT:NEXT
  170 PROCS
  180 DEFPROCS
  190 G%=1
  200 COLOUR 135:CLS:COLOUR 4:PRINT '"        HELI    V1.7";
  210 PRINT '"   PHIL HITE 2021"
  220 PRINT '"O LEFT      P RIGHT"
  230 PRINT '"SPACE PICK UP/DROP"
  240 PRINT '"D VOL DOWN  U VOL UP"
  250 PRINT "M SOUND OFF H HOLD"
  260 PRINT '"  BUILD THE BRIDGE"
  270 PRINT "   STOP THE FLOOD"
  280 PRINT "BEWARE OF OCTOPUSES!"
  290 PRINT "S START     Q QUIT"
  300 PRINT '"  TODAY";CHR$(39);"S GREATEST"
  310 FOR Y%=0 TO 9:PRINT TAB(3-LEN(STR$(Y%+1)),Y%+20);Y%+1;TAB(4,Y%+20)H(Y%);TAB(11,Y%+20)"L";L(Y%);TAB(15,Y%+20);:FOR X%=0 TO 2:PRINT N$(Y%,X%);:NEXT:NEXT
  320 PROCK
  330 ENDPROC
  340 DEFPROCK
  350 REPEAT
  360   K$=INKEY$(0)
  370   IF K$="d" OR K$="u" OR K$="m" OR K$="i" OR K$="s" OR K$="q" K$=CHR$(ASC(K$)-32)
  380   IF K$="D" AND A%<0 A%=A%+1:SOUND 1,A%,101,1
  390   IF K$="U" AND A%>-15 A%=A%-1:SOUND 1,A%,101,1
  400   IF K$="M" AND Q%=1 Q%=0:R%=A%:A%=0:K$="" ELSE IF K$="M" Q%=1:A%=R%:K$=""
  410   IF G%=1 AND Q%=1 PRINT TAB(9,11)"FF" ELSE IF G%=1 PRINT TAB(9,11)"N "
  420   IF K$="I" AND G%=0 PROCS
  430   IF K$="S" COLOUR 135:CLS:COLOUR 4:S%=0:L%=1:W%=0:K%=0:U%=0:E%=0:PROCG:PROCC
  440 UNTIL K$="Q"
  450 END
  460 ENDPROC
  470 DEFPROCG
  480 COLOUR 4:PRINT TAB(3,8)"GET READY FOR";TAB(5,9)"LEVEL ";L%;"!":PROCP(19)
  490 SOUND 1,A%,101-49,12:PROCP(19):SOUND 2,A%,117-49,6:PROCP(19):SOUND 3,A%,129-49,3:PROCP(19)
  500 SOUND 1,A%,109-49,12:PROCP(19):SOUND 2,A%,121-49,6:PROCP(19):SOUND 3,A%,137-49,3:PROCP(19)
  510 SOUND 1,A%,129-49,12:PROCP(19):SOUND 2,A%,137-49,6:PROCP(19):SOUND 3,A%,145-49,3:PROCP(19)
  520 SOUND 1,A%,149-49,12:SOUND 2,A%,165-49,12:SOUND 3,A%,177-49,12
  530 CLS
  540 C%=9:D%=0
  550 ENDPROC
  560 DEFPROCC
  570 REPEAT
  580   I%=RND(8)-1:P%=128+RND(8)-1
  590 UNTIL I%<>7 AND P%<>128+7
  600 COLOUR I%:COLOUR P%
  610 FOR Y%=17 TO 31
  620   FOR X%=0 TO 4
  630     PRINT TAB(X%,Y%)CHR$(241);TAB(X%+15,Y%)CHR$(241);
  640   NEXT
  650 NEXT
  660 PRINT TAB(0,0)CHR$(11);
  670 COLOUR 7:COLOUR 128:PRINT "                    ";TAB(0,0)S%;TAB(8,0)"L"L%;TAB(12,0)"HI";H%
  680 COLOUR 5:COLOUR 135
  690 FOR X%=0 TO 4
  700   PRINT TAB(X%,17)CHR$(240);TAB(X%+15,17)CHR$(240);
  710 NEXT
  720 FOR X%=0 TO 19
  730   IF X%<5 OR X%>14 G(X%)=1 ELSE G(X%)=0
  740 NEXT
  750 V%=W%
  760 REPEAT
  770   REPEAT UNTIL INKEY(0)=-1
  780   COLOUR 2
  790   IF D%=0 PRINT TAB(C%,2)CHR$(248);CHR$(249) ELSE PRINT TAB(C%,2)CHR$(246);CHR$(247)
  800   COLOUR 1:PRINT TAB(C%+D%,3)CHR$(245)
  810   IF K%=1 COLOUR 5:PRINT TAB(C%+D%,4);CHR$(240)
  820   PROCB:PROCP(5):PROCW
  830   U%=INKEY(1)
  835   IF U%<>-1 PROCP(5)
  840   IF INKEY(-99) PROCM
  850   IF INKEY(-55) AND D%=0 AND C%>0 PRINT TAB(C%+1,2)" ";TAB(C%,3)" ";TAB(C%,4)" ":C%=C%-1 ELSE IF INKEY(-55) D%=0:PRINT TAB(C%+1,3)" ";TAB(C%+1,4)" "
  860   IF INKEY(-56) AND D%=1 AND C%<18 PRINT TAB(C%,2)" ";TAB(C%+1,3)" ";TAB(C%+1,4)" ":C%=C%+1 ELSE IF INKEY(-56) D%=1:PRINT TAB(C%,3)" ";TAB(C%,4)" "
  870   IF INKEY(-85) COLOUR 4:PRINT TAB(6,6)"HOLD IT!":U%=GET:PRINT TAB(6,6)"        "
  880   IF INKEY(-51) AND A%<0 A%=A%+1:SOUND 1,A%,101,1
  890   IF INKEY(-54) AND A%>-15 A%=A%-1:SOUND 1,A%,101,1
  900   IF INKEY(-102) AND Q%=1 U%=GET:Q%=0:R%=A%:A%=0
  910   IF INKEY(-102) AND Q%=0 U%=GET:Q%=1:A%=R%
  930 UNTIL INKEY(-17)
  940 PROCS
  950 ENDPROC
  960 DEFPROCB
  970 G%=1
  980 FOR X%=5 TO 14
  990   IF G(X%)=0 G%=0:X%=14
 1000 NEXT
 1010 IF G%=0 ENDPROC
 1020 COLOUR 4:PRINT TAB(5,5)"WELL DONE!";TAB(1,6)"BONUS=1000 X LEVEL"
 1030 Z%=1000*L%
 1040 IF Z%<10000 PRINT TAB(7,7);Z% ELSE PRINT TAB(6,7);Z%
 1050 S%=S%+Z%
 1060 PROCH
 1070 W%=W%+4:L%=L%+1
 1080 PROCG:PROCC
 1090 ENDPROC
 1100 DEFPROCP(J%)
 1110 T%=TIME+J%:REPEAT UNTIL TIME>=T%
 1120 ENDPROC
 1130 DEFPROCW
 1140 VDU 18,0,4
 1150 MOVE 320,V%
 1160 DRAW 959,V%
 1170 V%=V%+2
 1180 IF V%<448 ENDPROC
 1190 SOUND 0,A%,4,40
 1200 COLOUR 132
 1210 FOR Y%=31 TO 18 STEP -1
 1220   PRINT TAB(5,Y%)"          ";
 1230 NEXT
 1240 FOR Y%=17 TO 1 STEP -1
 1250   PRINT TAB(0,Y%)"                    "
 1260 NEXT
 1270 FOR F%=1 TO 150
 1280   X%=RND(10):Y%=RND(14):D%=RND(2)
 1290   COLOUR RND(16)-1
 1300   PRINT TAB(4+X%,32-Y%)CHR$(242+D%);
 1310   X%=RND(20):Y%=RND(17):D%=RND(2)
 1320   COLOUR RND(16)-1
 1330   PRINT TAB(X%-1,Y%)CHR$(242+D%);
 1340 NEXT
 1350 Y%=0
 1360 REPEAT
 1370   IF S%>H(Y%) E%=1
 1380   IF E%=1 AND Y%<9 H(Y%+1)=H(Y%):L(Y%+1)=L(Y%):FOR X%=0 TO 2:N$(Y%+1,X%)=N$(Y%,X%):NEXT
 1390   IF E%=0 Y%=Y%+1
 1400 UNTIL E%=1 OR Y%=10
 1410 COLOUR 7:COLOUR 132:PRINT TAB(5,18)"GAME OVER!"
 1420 IF E%=1 PROCI
 1430 COLOUR 7:COLOUR 132:PRINT TAB(5,20)"S START";TAB(5,22)"I INSTRUCT";TAB(5,24)"Q QUIT"
 1440 PROCK
 1450 ENDPROC
 1460 DEFPROCM
 1470 N%=3:M%=C%+D%
 1480 IF M%>4 AND M%<15 B%=17 ELSE B%=16
 1490 IF K%=1 AND G(M%)=1 B%=B%-1
 1500 REPEAT
 1510   COLOUR 0:PRINT TAB(M%,N%)CHR$(242):COLOUR 1:PRINT TAB(M%,N%+1)CHR$(245)
 1520   IF K%=1 COLOUR 5:PRINT TAB(M%,N%+2)CHR$(240)
 1530   SOUND 1,A%,-N%+54,1
 1540   PROCP(1)
 1550   N%=N%+1
 1560 UNTIL N%=B%
 1570 IF G(M%)=1 AND K%=0 G(M%)=0:K%=1:GOTO 1620
 1580 IF G(M%)=K% GOTO 1620
 1590 IF G(M%)=0 AND K%=1 AND (M%<6 OR M%>13) G(M%)=1:K%=0:GOTO 1610
 1600 IF (G(M%-1)=1 OR G(M%+1)=1) AND M%>5 AND M%<14 G(M%)=1:K%=0
 1610 IF G(M%)=1 AND M%>4 AND M%<15 S%=S%+100:PROCH
 1620 REPEAT
 1630   N%=N%-1
 1640   COLOUR 1:PRINT TAB(M%,N%)CHR$(245)
 1650   IF K%=0 PRINT TAB(M%,N%+1)" " ELSE COLOUR 5:PRINT TAB(M%,N%+1);CHR$(240)
 1660   IF K%=1 AND (G(M%)=0 OR (G(M%)=1 AND N%<B%)) PRINT TAB(M%,N%+2)" "
 1670   SOUND 1,A%,-N%+54,1
 1680   PROCP(1)
 1690 UNTIL N%=3
 1700 PROCO
 1710 ENDPROC
 1720 DEFPROCH
 1730 IF S%>H% H%=S%
 1740 COLOUR 7:COLOUR 128:PRINT TAB(0,0);S%;TAB(14,0);H%
 1750 COLOUR 135
 1760 ENDPROC
 1770 DEFPROCI
 1780 COLOUR 128:PRINT TAB(0,1)"HISCORE PRESS RETURN"
 1790 REPEAT
 1800   K$=INKEY$(0)
 1810 UNTIL K$=CHR$(13)
 1820 H(Y%)=S%:L(Y%)=L%
 1830 PRINT TAB(8,1)"INITIALS?   ";TAB(17,1);
 1840 FOR X%=0 TO 2
 1850   N$(Y%,X%)=" "
 1860 NEXT
 1870 FOR X%=0 TO 2
 1880   REPEAT
 1890     I$=GET$
 1900     I%=ASC(I$)
 1910     IF I$>="a" AND I$<="z" I%=I%-32
 1920     IF I%=13 X%=2
 1930   UNTIL (I%>64 AND I%<91) OR I%=13
 1940   I$=CHR$(I%)
 1950   N$(Y%,X%)=I$
 1960   PRINT I$;
 1970 NEXT
 1980 PROCS
 1990 ENDPROC
 2000 DEFPROCO
 2010 M%=C%+D%:O%=RND(2)-1
 2020 O%=RND(4-O%)
 2030 IF NOT (O%=1 AND K%=1 AND G(M%)=0 AND (M%<5 OR M%>14)) ENDPROC
 2040 REPEAT
 2050   I%=RND(16)-1
 2060 UNTIL I%<>7
 2070 FOR Y%=4 TO 16
 2080   PRINT TAB(M%,Y%)" "
 2090   COLOUR 5:PRINT TAB(M%,Y%+1)CHR$(240)
 2100   IF Y%<16 COLOUR I%:PRINT TAB(M%,Y%+2)CHR$(250)
 2110   SOUND 1,A%,-Y%+216,1
 2120   PROCP(1)
 2130 NEXT
 2140 K%=0:G(M%)=1
 2150 ENDPROC
 2160 REM *TAPE
 2170 REM FOR I%=0 TO TOP-PAGE STEP 4:I%!&E00=I%!PAGE:NEXT:?&13=?&13-(PAGE-&E00)DIV256:PAGE=&E00:LOMEM=TOP:RUN
