   10 REM HELI 1.91 BY PHIL HITE 2021
   20 IF PAGE>&E00 GOTO 2170
   30 MODE 2
   40 VDU 23,1,0;0;0;0;
   50 VDU 23,240,255,128,65,34,20,8,255,0
   60 VDU 23,241,170,85,170,85,170,85,170,85
   70 VDU 23,242,24,36,24,36,24,36,24,36
   80 VDU 23,243,2,0,28,186,254,190,28,0
   90 VDU 23,244,128,0,112,186,254,250,112,0
  100 VDU 23,245,60,126,255,231,231,231,165,231
  110 VDU 23,246,7,0,0,128,127,1,0,0
  120 VDU 23,247,255,32,112,236,226,252,80,252
  130 VDU 23,248,255,4,14,55,71,63,10,63
  140 VDU 23,249,224,0,0,1,254,128,0,0
  150 VDU 23,250,170,170,170,170,254,214,124,56
  160 DIM G(19):H%=0:A%=-15:R%=A%:Q%=1:DIM H(9):DIM L(9):DIM N$(9,2):FOR Y%=0 TO 9:L(Y%)=1:FOR X%=0 TO 2:N$(Y%,X%)="A":NEXT:NEXT
  170 PROCS
  180 DEFPROCS
  190 G%=1:T$=" TODAY'S GREATEST"
  200 COLOUR 135:CLS:COLOUR 4:PRINT '"        HELI    1.91"
  210 PRINT "   PHIL HITE 2021"
  220 PRINT '"O LEFT      P RIGHT"
  230 PRINT '"SPACE PICK UP/DROP"
  240 PRINT '"D VOL DOWN  U VOL UP"
  250 PRINT "M SOUND OFF H HOLD"
  260 PRINT '"  BUILD THE BRIDGE"
  270 PRINT "   STOP THE FLOOD"
  280 PRINT "BEWARE OF OCTOPUSES!"
  290 PRINT "S START     Q QUIT"
  300 PRINT '" ";T$
  310 FOR Y%=0 TO 9:PRINT TAB(3-LEN(STR$(Y%+1)),Y%+20);Y%+1;TAB(4,Y%+20)H(Y%);TAB(11,Y%+20)"L";L(Y%);TAB(15,Y%+20);:FOR X%=0 TO 2:PRINT N$(Y%,X%);:NEXT:NEXT
  320 PROCK
  330 ENDPROC
  340 DEFPROCK
  350 REPEAT
  360   K$=INKEY$(0)
  370   IF K$="d" OR K$="u" OR K$="m" OR K$="i" OR K$="s" OR K$="q" K$=CHR$(ASC(K$)-32)
  380   IF K$="D" AND A%<0 A%=A%+1:SOUND 1,A%,101,1
  390   IF K$="U" AND A%>-15 A%=A%-1:SOUND 1,A%,101,1
  400   IF K$="M" AND Q%=1 Q%=0:R%=A%:A%=0:K$="" ELSE IF K$="M" Q%=1:A%=R%:K$=""
  410   IF G%=1 AND Q%=1 PRINT TAB(9,11)"FF" ELSE IF G%=1 PRINT TAB(9,11)"N "
  420   IF K$="I" AND G%=0 PROCS
  430   IF K$="S" COLOUR 135:CLS:COLOUR 4:S%=0:L%=1:W%=0:K%=0:U%=0:E%=0:PROCG:PROCC
  440   IF G%=1 T$=RIGHT$(T$,16)+LEFT$(T$,1):PROCP(16):PRINT TAB(1,19)T$
  450 UNTIL K$="Q"
  460 END
  470 ENDPROC
  480 DEFPROCG
  490 COLOUR 4:PRINT TAB(3,8)"GET READY FOR";TAB(5,9)"LEVEL ";L%;"!":PROCP(19)
  500 SOUND 1,A%,101-49,12:PROCP(19):SOUND 2,A%,117-49,6:PROCP(19):SOUND 3,A%,129-49,3:PROCP(19)
  510 SOUND 1,A%,109-49,12:PROCP(19):SOUND 2,A%,121-49,6:PROCP(19):SOUND 3,A%,137-49,3:PROCP(19)
  520 SOUND 1,A%,129-49,12:PROCP(19):SOUND 2,A%,137-49,6:PROCP(19):SOUND 3,A%,145-49,3:PROCP(19)
  530 SOUND 1,A%,149-49,12:SOUND 2,A%,165-49,12:SOUND 3,A%,177-49,12
  540 CLS
  550 C%=9:D%=0
  560 ENDPROC
  570 DEFPROCC
  580 REPEAT
  590   I%=RND(8)-1:P%=128+RND(8)-1
  600 UNTIL I%<>7 AND P%<>128+7
  610 COLOUR I%:COLOUR P%
  620 FOR Y%=17 TO 31
  630   FOR X%=0 TO 4
  640     PRINT TAB(X%,Y%)CHR$(241);TAB(X%+15,Y%)CHR$(241);
  650   NEXT
  660 NEXT
  670 PRINT TAB(0,0)CHR$(11);
  680 COLOUR 7:COLOUR 128:PRINT "                    ";TAB(0,0)S%;TAB(8,0)"L"L%;TAB(12,0)"HI";H%
  690 COLOUR 5:COLOUR 135
  700 FOR X%=0 TO 4
  710   PRINT TAB(X%,17)CHR$(240);TAB(X%+15,17)CHR$(240);
  720 NEXT
  730 FOR X%=0 TO 19
  740   IF X%<5 OR X%>14 G(X%)=1 ELSE G(X%)=0
  750 NEXT
  760 V%=W%
  770 REPEAT
  780   REPEAT UNTIL INKEY(0)=-1
  790   COLOUR 2
  800   IF D%=0 PRINT TAB(C%,2)CHR$(248);CHR$(249) ELSE PRINT TAB(C%,2)CHR$(246);CHR$(247)
  810   COLOUR 1:PRINT TAB(C%+D%,3)CHR$(245)
  820   IF K%=1 COLOUR 5:PRINT TAB(C%+D%,4);CHR$(240)
  830   PROCP(1)
  840   PROCB:PROCP(10):PROCW
  850   IF INKEY(-99) PROCM
  860   IF INKEY(-55) AND D%=0 AND C%>0 PRINT TAB(C%+1,2)" ";TAB(C%,3)" ";TAB(C%,4)" ":C%=C%-1 ELSE IF INKEY(-55) D%=0:PRINT TAB(C%+1,3)" ";TAB(C%+1,4)" "
  870   IF INKEY(-56) AND D%=1 AND C%<18 PRINT TAB(C%,2)" ";TAB(C%+1,3)" ";TAB(C%+1,4)" ":C%=C%+1 ELSE IF INKEY(-56) D%=1:PRINT TAB(C%,3)" ";TAB(C%,4)" "
  880   IF INKEY(-85) COLOUR 4:PRINT TAB(6,6)"HOLD IT!":U%=GET:PRINT TAB(6,6)"        "
  890   IF INKEY(-51) AND A%<0 A%=A%+1:SOUND 1,A%,101,1:Q%=1
  900   IF INKEY(-54) AND A%>-15 A%=A%-1:SOUND 1,A%,101,1:Q%=1
  910   IF INKEY(-102) AND Q%=1 :Q%=0:R%=A%:A%=0:PROCP(45)
  920   IF INKEY(-102) AND Q%=0 :Q%=1:A%=R%:PROCP(45)
  930 UNTIL INKEY(-17)
  940 REPEAT UNTIL INKEY(0)=-1
  950 PROCS
  960 ENDPROC
  970 DEFPROCB
  980 G%=1
  990 FOR X%=5 TO 14
 1000   IF G(X%)=0 G%=0:X%=14
 1010 NEXT
 1020 IF G%=0 ENDPROC
 1030 COLOUR 4:PRINT TAB(5,5)"WELL DONE!";TAB(1,6)"BONUS=1000 X LEVEL"
 1040 Z%=1000*L%
 1050 IF Z%<10000 PRINT TAB(7,7);Z% ELSE PRINT TAB(6,7);Z%
 1060 S%=S%+Z%
 1070 PROCH
 1080 W%=W%+4:L%=L%+1
 1090 PROCG:PROCC
 1100 ENDPROC
 1110 DEFPROCP(J%)
 1120 T%=TIME+J%:REPEAT UNTIL TIME>=T%
 1130 ENDPROC
 1140 DEFPROCW
 1150 VDU 18,0,4
 1160 MOVE 320,V%
 1170 DRAW 959,V%
 1180 V%=V%+2
 1190 IF V%<448 ENDPROC
 1200 SOUND 0,A%,4,40
 1210 COLOUR 132
 1220 FOR Y%=31 TO 18 STEP -1
 1230   PRINT TAB(5,Y%)"          ";
 1240 NEXT
 1250 FOR Y%=17 TO 1 STEP -1
 1260   PRINT TAB(0,Y%)"                    "
 1270 NEXT
 1280 FOR F%=1 TO 150
 1290   X%=RND(10):Y%=RND(14):D%=RND(2)
 1300   COLOUR RND(16)-1
 1310   PRINT TAB(4+X%,32-Y%)CHR$(242+D%);
 1320   X%=RND(20):Y%=RND(17):D%=RND(2)
 1330   COLOUR RND(16)-1
 1340   PRINT TAB(X%-1,Y%)CHR$(242+D%);
 1350 NEXT
 1360 Y%=0
 1370 REPEAT
 1380   IF S%>H(Y%) E%=1
 1390   IF E%=1 AND Y%<9 H(Y%+1)=H(Y%):L(Y%+1)=L(Y%):FOR X%=0 TO 2:N$(Y%+1,X%)=N$(Y%,X%):NEXT
 1400   IF E%=0 Y%=Y%+1
 1410 UNTIL E%=1 OR Y%=10
 1420 COLOUR 7:COLOUR 132:PRINT TAB(5,18)"GAME OVER!"
 1430 IF E%=1 PROCI
 1440 COLOUR 7:COLOUR 132:PRINT TAB(5,20)"S START";TAB(5,22)"I INSTRUCT";TAB(5,24)"Q QUIT"
 1450 PROCK
 1460 ENDPROC
 1470 DEFPROCM
 1480 N%=3:M%=C%+D%
 1490 IF M%>4 AND M%<15 B%=17 ELSE B%=16
 1500 IF K%=1 AND G(M%)=1 B%=B%-1
 1510 REPEAT
 1520   COLOUR 0:PRINT TAB(M%,N%)CHR$(242):COLOUR 1:PRINT TAB(M%,N%+1)CHR$(245)
 1530   IF K%=1 COLOUR 5:PRINT TAB(M%,N%+2)CHR$(240)
 1540   SOUND 1,A%,-N%+54,1
 1550   PROCP(1)
 1560   N%=N%+1
 1570 UNTIL N%=B%
 1580 IF G(M%)=1 AND K%=0 G(M%)=0:K%=1:GOTO 1630
 1590 IF G(M%)=K% GOTO 1630
 1600 IF G(M%)=0 AND K%=1 AND (M%<6 OR M%>13) G(M%)=1:K%=0:GOTO 1620
 1610 IF (G(M%-1)=1 OR G(M%+1)=1) AND M%>5 AND M%<14 G(M%)=1:K%=0
 1620 IF G(M%)=1 AND M%>4 AND M%<15 S%=S%+100:PROCH
 1630 REPEAT
 1640   N%=N%-1
 1650   COLOUR 1:PRINT TAB(M%,N%)CHR$(245)
 1660   IF K%=0 PRINT TAB(M%,N%+1)" " ELSE COLOUR 5:PRINT TAB(M%,N%+1);CHR$(240)
 1670   IF K%=1 AND (G(M%)=0 OR (G(M%)=1 AND N%<B%)) PRINT TAB(M%,N%+2)" "
 1680   SOUND 1,A%,-N%+54,1
 1690   PROCP(1)
 1700 UNTIL N%=3
 1710 PROCO
 1720 ENDPROC
 1730 DEFPROCH
 1740 IF S%>H% H%=S%
 1750 COLOUR 7:COLOUR 128:PRINT TAB(0,0);S%;TAB(14,0);H%
 1760 COLOUR 135
 1770 ENDPROC
 1780 DEFPROCI
 1790 COLOUR 128:PRINT TAB(0,1)"HISCORE PRESS RETURN"
 1800 REPEAT
 1810   K$=INKEY$(0)
 1820 UNTIL K$=CHR$(13)
 1830 H(Y%)=S%:L(Y%)=L%
 1840 PRINT TAB(8,1)"INITIALS?   ";TAB(17,1);
 1850 FOR X%=0 TO 2
 1860   N$(Y%,X%)=" "
 1870 NEXT
 1880 FOR X%=0 TO 2
 1890   REPEAT
 1900     I$=GET$
 1910     I%=ASC(I$)
 1920     IF I$>="a" AND I$<="z" I%=I%-32
 1930     IF I%=13 X%=2
 1940   UNTIL (I%>64 AND I%<91) OR I%=13
 1950   I$=CHR$(I%)
 1960   N$(Y%,X%)=I$
 1970   PRINT I$;
 1980 NEXT
 1990 PROCS
 2000 ENDPROC
 2010 DEFPROCO
 2020 M%=C%+D%:O%=RND(2)-1
 2030 O%=RND(4-O%)
 2040 IF NOT (O%=1 AND K%=1 AND G(M%)=0 AND (M%<5 OR M%>14)) ENDPROC
 2050 REPEAT
 2060   I%=RND(16)-1
 2070 UNTIL I%<>7
 2080 FOR Y%=4 TO 16
 2090   PRINT TAB(M%,Y%)" "
 2100   COLOUR 5:PRINT TAB(M%,Y%+1)CHR$(240)
 2110   IF Y%<16 COLOUR I%:PRINT TAB(M%,Y%+2)CHR$(250)
 2120   SOUND 1,A%,-Y%+216,1
 2130   PROCP(1)
 2140 NEXT
 2150 K%=0:G(M%)=1
 2160 ENDPROC
 2170 *TAPE
 2180 FOR I%=0 TO TOP-PAGE STEP 4:I%!&E00=I%!PAGE:NEXT:?&13=?&13-(PAGE-&E00)DIV256:PAGE=&E00:LOMEM=TOP:RUN
